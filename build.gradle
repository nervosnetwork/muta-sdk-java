plugins {
    id 'java-library'
    id 'maven-publish'
    id 'com.github.sherter.google-java-format' version '0.9'
    id "com.jfrog.bintray" version "1.8.5"
}

group 'org.nervos'
version '0.2.0-beta.2.1'

repositories {
    mavenCentral()
    jcenter()
    mavenLocal()
}
sourceCompatibility = "1.8"
targetCompatibility = "1.8"

java {
    withSourcesJar()
    withJavadocJar()
}

test {
    useJUnitPlatform()
}

compileJava.dependsOn(tasks.googleJavaFormat)
compileTestJava.dependsOn(tasks.googleJavaFormat)
verifyGoogleJavaFormat.mustRunAfter(tasks.googleJavaFormat)

googleJavaFormat {
    options style: 'AOSP'
}

dependencies {
    api "com.squareup.okhttp3:okhttp:4.7.2"
    api "com.squareup.okhttp3:logging-interceptor:4.7.2"

    api group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.11.0'

    //implementation group: 'org.bouncycastle', name: 'bcprov-debug-jdk15on', version: '1.65' // included by org.web3j:rlp
    implementation group: 'org.web3j', name: 'rlp', version: '5.0.0'
    implementation group: 'org.web3j', name: 'core', version: '5.0.0'

    annotationProcessor group: 'org.projectlombok', name: 'lombok', version: '1.18.12'
    compileOnly group: 'org.projectlombok', name: 'lombok', version: '1.18.12'
    testAnnotationProcessor group: 'org.projectlombok', name: 'lombok', version: '1.18.12'
    testCompileOnly group: 'org.projectlombok', name: 'lombok', version: '1.18.12'

    api group: 'org.slf4j', name: 'slf4j-api', version: '1.7.30'
    testImplementation group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'
    testImplementation group: 'ch.qos.logback', name: 'logback-core', version: '1.2.3'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.6.2'

}

bintray {
    user = pubBintrayUser
    key = pubBintrayApiKey
    publications = ['normal']
    pkg {
        repo = 'Muta-Java-SDK'
        name = 'muta-java-sdk'
        userOrg = user
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/nervosnetwork/muta-sdk-java'
        desc = 'This is Muta-Chain JAVA SDK'
        version {
            name = project.version
            desc = 'Muta Java SDK'
        }
    }
}

publishing {
    publications {
        normal(MavenPublication) {
            from components.java
        }
    }
}

// only do it before release
task copyDocTask(dependsOn: javadoc) {
    doLast {
        delete{
            delete fileTree("docs")
        }

        copy {

            from( fileTree('build/docs/javadoc'))

            into 'docs/'
        }
    }
}
